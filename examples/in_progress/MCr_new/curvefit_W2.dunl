#yield = change in aa of x / change in umol S
# x is always in grams
# 


;Resource
#Adapted from the matlab file in the supp. material
#Additional states added for external substrate and biomass
#States have been renamed so all proteins are capitalized

;;states
x  : [0.1]*2
s  : [1000]*2
si : [0]*2
p  : [0]*2

mQ : [0]*2
mT : [0]*2
mM : [0]*2
mR : [0]*2
mH : [0]*2

RmQ : [0]*2
RmT : [0]*2
RmM : [0]*2
RmR : [0]*2
RmH : [0]*2

Q : [0.4]*2
T : [0]*2
M : [0]*2
R : [0.115]*2
H : [0.03]*2

;;events
init : [
	trigger : time == 0,
	assign :[
		M  = M0,
		T  = 1-R-H-Q-M,
		mQ = Q,
		mT = T,
		mM = M,
		mR = R,
		mH = H
		]
	]

;;parameters
# v_uptake    : [13.660]*2
# k_uptake    : [1.220e+03]*2
# v_biosyn    : [13.475]*2
# k_biosyn    : [0.110]*2
# Yield       : [9.625]*2
# w_Q         : [1.000]*2
# w_T         : [0.278]*2
# w_M         : [0.278]*2
# w_R         : [0.144]*2
# w_H         : [0.769]*2
# k_mQ        : [0.030]*2
# k_mT        : [0.030]*2
# k_mM        : [0.030]*2
# k_mR        : [2.521e-03]*2
# k_mH        : [0.082]*2
# n_q         : [4.000]*2
# k_Q         : [7.023e+05]*2
# deg_m       : [0.100]*2
# ind         : [0.000, 1.000]
# bind_R      : [2.220e+04]*2
# unbind_R    : [7.409]*2
# v_max       : [1.580e+03]*2
# k_p         : [964.316]*2
# aa_per_H    : [227.000]*2
# M0          : [0.015]*2
# k_fH        : [80.290]*2
# n_fH        : [1.000]*2

# #After fitting
v_uptake    : [11.7189]*2
k_uptake    : [410.0615]*2
v_biosyn    : [12.8073]*2
k_biosyn    : [0.3295]*2
Yield       : [9.7929]*2
w_Q         : [1.0000]*2
w_T         : [8.6725]*2
w_M         : [2.1697]*2
w_R         : [8.2840e-03]*2
w_H         : [0.4816]*2
k_mQ        : [0.0300]*2
k_mT        : [7.1360]*2
k_mM        : [3.6202]*2
k_mR        : [0.1923]*2
k_mH        : [0.0820]*2
n_q         : [4.0000]*2
k_Q         : [1.4641e-03]*2
deg_m       : [0.1000]*2
ind         : [0.0000, 1.0000]
bind_R      : [4.4564e+03]*2
unbind_R    : [4.1291e-03]*2
v_max       : [1.5800e+03]*2
k_p         : [909.7348]*2
aa_per_H    : [227.0000]*2
M0          : [0.0181]*2
k_fH        : [13.0389]*2
n_fH        : [1.0000]*2

;;variables
#Physiology and conversion factors
aa_per_cell      : 1.0e8 
aa_per_R         : 7549.0
aa_per_prot      : 300.0
umol_aa_per_cell : aa_per_cell/6e17


n{{i}} : {{i}}/7459
	$$i: R, RmQ, RmR, RmH, RmT, RmM
n{{i}} : {{i}}/300
	$$i: Q, T, M

#Fractions
R_frac : R + RmR + RmH + RmT + RmM + RmQ
H_frac : H

#Conversion between cell mass and aa
x2aa_umol    : 1/110*1e6
x_in_aa_umol : x*x2aa_umol

#Fluxes
uptake : nT*v_uptake*s /(k_uptake + s)
biosyn : nM*v_biosyn*si/(k_biosyn + si)

#Translation and growth
k_synprot : v_max/k_p
gamma   : v_max*p/(k_synprot + p)
activeR : nRmQ + nRmR + nRmH + nRmT + nRmM
synprot : activeR*gamma
mu      : synprot
A       : mu/(v_max/aa_per_R * R_frac)

#Ribosome binding
binding_{{i}} : bind_R*nR*m{{i}} -unbind_R*Rm{{i}}
	$$i: Q, T, M, R, H
eff_binding: +binding_Q +binding_T +binding_M +binding_R +binding_H

termination_{{i}} : gamma/{{ii}}*Rm{{i}}
	$$i: Q,           T,           M,           R,        H
	$$ii: aa_per_prot, aa_per_prot, aa_per_prot, aa_per_R, aa_per_prot
eff_termination: +termination_Q +termination_T +termination_M +termination_R +termination_H

#Translation for each pool
syn_{{i}} : gamma*nRm{{i}}
	$$i: Q,           T,        M,              R,               H

;;rates
#Biomass and substrates
x  : mu*x
s  :                      -uptake*x_in_aa_umol
si : -mu*si -biosyn       +uptake
p  : -mu*p  +biosyn*Yield -synprot

#mRNA
mQ : w_Q*p/(k_mQ + p)/(1 + (Q/k_Q)**n_q) -binding_Q -deg_m*mQ -mu*mQ

m{{i}} : w_{{ii}}*p/({{iii}} + p) -binding_{{i}} -deg_m*m{{i}} -mu*m{{i}}
	$$i:  T, M, R, H
	$$ii: T, M, R, H*ind*k_fH**n_fH/(k_fH**n_fH+s**n_fH)
	$$iii: k_mT, k_mM, k_mR, k_mH


#Ribosome-mRNA polysome
Rm{{i}} : +binding_{{i}}-termination_{{i}}-mu*Rm{{i}}
	$$i: Q,           T,              M,              R,               H

#Proteins
{{i}} : syn_{{i}} -mu*{{i}}
	$$i: Q, T, M, H
R : syn_R -mu*R -eff_binding +eff_termination

;;int_args
# method: RK45
method : BDF

;;tspan
0 : !linspace, 0, 900, 61!
1 : !linspace, 0, 900, 61!

;;sim_args
line_args : [
	marker : None,
	linestyle : -,
	label : scenario,
	color : [
		0 : coral, 
		1 : cobalt, 
		2 : ocean,
		3 : steel
		]
	]

;;data_args;dataset
line_args : [
	color : [
		0 : cobalt, 
		1 : coral, 
		],
	capsize : 5
	]
	
;;optim_args
free_params:[
	v_uptake : [bounds : [1e1,  1e3 ],  scale : log10],
	k_uptake : [bounds : [1e0,  2e3 ],  scale : log10],
	v_biosyn : [bounds : [1e1,  1e4 ],  scale : log10],
	k_biosyn : [bounds : [1e-2, 1e1 ],  scale : log10],
	k_p      : [bounds : [1e1,  1e3 ],  scale : lin  ],
	w_T      : [bounds : [1e-3, 1e2 ],  scale : log10],
	w_M      : [bounds : [1e-3, 1e2 ],  scale : log10],
	w_R      : [bounds : [1e-3, 1e2 ],  scale : log10],
	w_H      : [bounds : [1e-3, 1e0 ],  scale : log10],
	k_mT     : [bounds : [1e-3, 1e1 ],  scale : log10],
	k_mM     : [bounds : [1e-3, 1e1 ],  scale : log10],
	k_mR     : [bounds : [1e-3, 1e1 ],  scale : log10],
	k_Q      : [bounds : [1e-3, 1e6 ],  scale : log10],
	k_fH     : [bounds : [1e1, 1e4  ],  scale : log10],
	bind_R   : [bounds : [1e1,  1e5 ],  scale : log10],
	unbind_R : [bounds : [1e-3, 1e2 ],  scale : log10],
	M0       : [bounds : [1e-2, 3e-1],  scale : lin  ],
	]
# #For predicting new scenarios
# free_params:[
	# v_uptake : [bounds : [1e1,  1e3 ], scale : log10],
	# k_uptake : [bounds : [1e0,  2e3 ], scale : log10],
	# v_biosyn : [bounds : [1e1,  1e4 ], scale : log10],
	# k_biosyn : [bounds : [1e-2, 1e1 ], scale : log10],
	# w_T      : [bounds : [1e-3, 1e2 ], scale : log10],
	# w_M      : [bounds : [1e-3, 1e2 ], scale : log10],
	# w_H      : [bounds : [1e-3, 1e0 ], scale : log10],
	# k_mT     : [bounds : [1e-3, 1e1 ], scale : log10],
	# k_mM     : [bounds : [1e-3, 1e1 ], scale : log10],
	# k_fH     : [bounds : [1e1, 1e4  ], scale : log10],
	# ]
settings : [
	disp  : True,
	niter : 15000
	]

