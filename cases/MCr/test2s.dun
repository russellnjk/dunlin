	
`MCr1
``states
{state} : [<{scenario}: {{init}}>]
	!scenario, 0, 1
	~state, x,    S,   I, P, Q,    R,    T,    M,    U,   nH, H, G, indi
	~init,  0.01, 1e3, 0, 0, 0.45, 0.03, 0.05, 0.25, 0.1, 0,  0, 0, 0

``params
v_uptake   : [100]*2
k_uptake   : [5e1]*2
v_synp     : [1e3]*2
k_synp     : [1]*2
yield_S    : [6.6]*2
v_synprot  : [1080]*2
k_synprot  : [1]*2
k_PR       : [0.3]*2
k_fr       : [1e-4]*2
n_fr       : [2]*2
k_fm       : [1]*2
n_fm       : [1]*2
fq         : [0.45]*2
fr         : [1]*2
ft         : [0.25]*2
fm         : [0.25]*2
fu         : [0.4]*2
ind        : [0, 1]
f_ind      : [0.1]*2
matH       : [0.01]*2
Jind       : [0.01]*2

;Definitions
;v_uptake : Turnover number for uptake lumped with average mw of transporter

``vrbs
;Proteins from aa
n_Q : Q/300
n_R : R/7459
n_T : T/300
n_M : M/300
n_H : H/230
;Conversion between cell mass and aa
x2aa_umol    : 1/110*1e6
x_in_aa_umol : x*x2aa_umol
;Uptake per umol of x_in_aa_umol
uptake       : v_uptake*n_T*S/(k_uptake + S)
synp         : v_synp  *n_M*I/(k_synp   + I)
;Translation aa/min in 1 umol of x
rsat    : P/(P+k_synprot)
synprot : v_synprot*n_R*rsat
mu      : synprot
;R regulation
PR       : 1-rsat
synG     : 60*n_R*PR/(k_PR + PR)
regR     : k_fr**n_fr/(k_fr**n_fr + G**n_fr)
regM     : k_fm**n_fm/(k_fm**n_fm + P**n_fm)
tfR      : fr*regR
tfT      : ft
tfM      : fm*regM
tfU      : fu
tfH      : f_ind*indi
synQ     : synprot*fq
syn{i}   : synprot*(1-fq)*tf{i} /(tfR + tfT + tfM + tfU + tfH)
	~i, R, T, M, U, H

``rts
S    : -uptake*x_in_aa_umol
x    : +x*mu 
I    : 0
P    : -mu*P -synprot +synp*yield_S +uptake*yield_S 
Q    : -mu*Q +synQ
R    : -mu*R +synR
T    : -mu*T +synT
M    : -mu*M +synM
U    : -mu*U +synU
nH   : -mu*nH +synH -matH*nH
H    : -mu*H +matH*nH
G    : -mu*G +synG -2.1*G
indi : -mu*indi +Jind*(ind-indi)

; ``events
; e0: [t > 100, fr = 0.01]

``exvs
uptake:
	return t, uptake

regR:
	return t, regR

regM:
	return t, regM

rfrac: 
	return t, R/(Q+R+M+H)

synprot:
	return t, synprot

mu:
	return t, mu

rsat:
	return t, rsat

PR:
	return t, PR

tfR:
	return t, tfR

tfM:
	return t, tfM

total_R:
	return t, R

``sim_args
line_args : [
	label : scenario,
	color : [
		0 : coral, 
		1 : cobalt, 
		2 : ocean,
		3 : steel
		]
	]
;base color for C0: cobalt, C1: ocean

``tspan
{scenario}: [linspace(0, 400, 41)]
	~scenario, 0, 1, 2, 3

``optim_args
free_params : [
	v_uptake  : [bounds : [1e1,  5e3 ], scale : log10],
	k_uptake  : [bounds : [1e1,  2e4 ], scale : log10],
	k_synprot : [bounds : [1e-4, 1e1 ], scale : log10],
	k_PR      : [bounds : [1e-2, 1e-0], scale : lin  ],
	k_fr      : [bounds : [1e-5, 1e-3], scale : log10],
	ft        : [bounds : [1e-2, 1e1 ], scale : lin  ],
	fm        : [bounds : [1e-2, 1e1 ], scale : lin  ],
	fu        : [bounds : [1e-2, 1e1 ], scale : lin  ],
	f_ind     : [bounds : [1e-2, 1e1 ], scale : lin  ],
	matH      : [bounds : [1e-3, 1e-1], scale : log10],
	Jind      : [bounds : [1e-3, 1e-1], scale : log10]
	]
settings : [
	disp  : True,
	x0    : [2, 3, -2, 0.2, -4, 0.3, 0.6, 0.6, 0.3, -2, -2],
	niter : 200000
	]

