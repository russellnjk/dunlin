[coarse_1]
;All conc in um
;All time in min
;Biomass in 1e9 cells/mL
use_numba = True

states = 
	x = [0.1],
	S = [1e5],
	P = [4e7],
	Q = [6.75e8],
	R = [5e8],
	M = [3.25e8],
	H = [0]
	
params = 
	Vrctr     = [0.001],
	Mcell     = [15e8],
	Dcell     = [1.7e6],
	v_uptake  = [726],
	k_uptake  = [1e5],
	t_frac    = [0.2],
	yield_S   = [0.5],
	v_synprot = [1020],
	k_synprot = [200],
	k_fr      = [8e-2],
	n_fr      = [0], 
	fq        = [0.45],
	fr        = [1],
	fm        = [0.65],
	fh        = [0],
	k_ind     = [0.5],
	k_div     = [1e7]

inputs =
	dil  = [[2.5e-2]],
	Sin  = [[1e5 ]],
	ind  = [[1   ]]

tspan = 
	linspace(0, 1e4, 41)
	
equations = 
	n_H = H/300
	n_Q = Q/300
	n_M = M/300
	n_R = R/7459
	
	#Cell density (protein conc)in uM
	#Cell volume in L
	all_prot = Q +R +M +H
	Vcell    = (all_prot*1e6/6e23)/Dcell
	
	#Uptake
	#Need to convert OD to cells
	n_cells      = Vrctr*x*1e9*1e3
	n_T          = n_M*t_frac
	uptake       = v_uptake*n_T*S/(S + k_uptake)
	total_uptake = uptake*n_cells/6e23*1e6/Vrctr
	
	#Translation
	#Need to convert from molecs to umol
	P_conc  = (P*1e6/6e23)/Vcell
	r_sat   = P_conc/(P_conc + k_synprot) 
	synprot = v_synprot*n_R*r_sat

	#R feedback	
	PR  = P/R
	reg = PR**n_fr/(k_fr**n_fr + PR**n_fr) 
	fr_ = fr*reg
	
	#Induction
	fh_ = fh*ind/(k_ind + ind)
	
	#Split pools
	synP = uptake*yield_S
	synQ = synprot*fq
	synM = synprot*(1-fq)*fm /(fm + fr_ + fh)
	synR = synprot*(1-fq)*fr_/(fm + fr_ + fh)
	synH = synprot*(1-fq)*fh /(fm + fr_ + fh)
	
	#Growth rate
	#mu = (synQ +synM +synR)/Mcell
	prot = Q + M + R
	add  = prot
	mu   = (synQ +synM +synR)/add
	
	dS = -dil*S +dil*Sin -total_uptake
	dx = -dil*x +x*mu 
	dP = -mu*P +synP -synprot
	dQ = -mu*Q +synQ
	dR = -mu*R +synR
	dM = -mu*M +synM
	dH = -mu*H +synH

objective_n_R = 
	return t.values, n_R.values

objective_PR = 
	return t.values, PR.values
	
objective_r_sat = 
	return t.values, r_sat.values

objective_mu = 
	return t.values, mu.values

objective_reg = 
	return PR.values, reg.values, '+'
	
